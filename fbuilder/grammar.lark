start: definition*

definition: code_block
    | code_definition
    | macro_definition
    | word_definition

code_block: "codeblock" code_line* "end"

code_definition: "defcode" IDENTIFIER code_line* "end"

macro_definition: "macro" IDENTIFIER code_line* "end"

word_definition: "defword" IDENTIFIER word* "end"

code_line: instruction
    | macro_call

instruction: OPCODE ( "." SIZESUFFIX )? [ paramlist ]

paramlist: param ( "," param )*

param: REGISTER
    | IMMEDIATE_NUMBER
    | "[" REGISTER "]"

macro_call: "$" IDENTIFIER "(" ")"

word: WORD_NAME

OPCODE: "add"
    | "dump"
    | "illegal"
    | "input"
    | "inc_wp"
    | "jmp"
    | "mov"
    | "nop"
    | "output"
    | "popd"
    | "popr"
    | "pushd"
    | "pushr"
    | "terminate"

REGISTER: "%ip"
    | "%wp"
    | "%rsp"
    | "%dsp"
    | "%acc1"
    | "%acc2"

IMMEDIATE_NUMBER: "#" NUMBER

WORD_NAME: /[\x21-\x7E]+/i
IDENTIFIER: /[a-zA-Z0-9_]+/
SIZESUFFIX: /[bw]/i

%import common.NEWLINE
COMMENT: "//" /(.)+/ NEWLINE
%ignore COMMENT

%import common.LETTER
%import common.INT -> NUMBER
%import common.WS
%ignore WS